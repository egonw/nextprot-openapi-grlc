#+ summary: Lists proteins with associated pathways in WikiPathways
#+ endpoint_in_url: False
#+ tags:
#+   - Federation with WikiPathways

PREFIX wp: <http://vocabularies.wikipathways.org/wp#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX : <http://nextprot.org/rdf#>

select distinct ?entry ?gen ?pathwayname where {
    service <http://85.214.42.229:8890/sparql> {

        {?geneProduct a wp:Protein}
            union
        {?geneProduct a wp:GeneProduct}
  
        ?geneProduct rdfs:label ?gen .
        filter(!regex(?gen,"[ a-z-]")). # ensures official gene names for subsequent neXtprot matching

        ?geneProduct dcterms:isPartOf ?pathway .
        ?pathway a wp:Pathway .
        ?pathway wp:organism ?organism .
        filter(contains(str(?organism),"9606")) # Human proteins
        ?pathway dc:title ?pathwayname .
    }
    ?entry a :Entry .
    ?entry :gene / :name ?gen .
}
order by ?pathwayname
